;------------------------------------------------------------------------------
; Action! 3.7P BY JAC! https://atariwiki.org/wiki/Wiki.jsp?page=Action
;------------------------------------------------------------------------------
; ATARI MEMORY MAP     https://www.atariarchives.org/mapping/memorymap.php
;------------------------------------------------------------------------------
; Atari800             https://atari800.github.io
; Graph2Font           http://g2f.atari8.info/instrukcja.html
; Atari FontMaker      https://sourceforge.net/projects/atari-fontmaker
; Sprite Editor XL     http://bocianu.atari.pl/blog/spriteXL
; Exomizer             https://bitbucket.org/magli143/exomizer/wiki/Home
;------------------------------------------------------------------------------
; Sublime Text 3       https://www.sublimetext.com/3
; Syntax highliting scheme for Action! ATARI language
; https://github.com/michalkolodziejski/ST3-action-syntax-highlighting
;------------------------------------------------------------------------------

MODULE

;------------------------------------------------------------------------------
; CODEBASE START
;------------------------------------------------------------------------------

SET $E=$1000
SET $491=$1000

;------------------------------------------------------------------------------
; DEFINED CONSTANTS
;------------------------------------------------------------------------------

; ML OPCODES
DEFINE
  RTI="$40",PHA="$48",PLA="$68",JMPI="$6C",STA="$8D",LDAI="$A9",
  CMPI="$C9",DEC="$CE",BNE="$D0"

; MEMORY MAP
DEFINE
  COLOR0="$2C4",COLOR1="$2C5",COLOR2="$2C6",COLOR3="$2C7",COLOR4="$2C8",
  COLPF0="$D016",COLPF1="$D017",COLPF2="$D018",COLPF3="$D019",COLBK="$D01A",
  SETVBV="$E45C"

; CUSTOM
DEFINE
  RFILL="14",LFILL="15",LSLOPE="70",RSLOPE="71",GLADE="77",
  STARTSCREEN="$6000",STARTROW="$210",LOOP="$200"

;------------------------------------------------------------------------------
; GLOBAL VARIABLES
;------------------------------------------------------------------------------

BYTE ARRAY
  DL=[
    $70
    $42 $49 $10
    $70 $F0
    $D2 $00 $00
    $92 $92 $92 $92 $92 $12 $12 $12 $12 $12 $12 $12 $12 $12 $12 $12 $12 $12
    $70 $70
    $42 $74 $10
    $41 $00 $10
  ],
  DLI=[
    PHA
    LDAI $8C STA $0A $D4 STA $18 $D0 DEC $27 $10 DEC $27 $10
    CMPI $80 BNE $A LDAI 0 STA $18 $D0 LDAI $8C STA $27 $10
    PLA RTI
  ]

BYTE ARRAY
  SCREEN=STARTSCREEN,
  TEXT1="             ascii scramble             ",
  TEXT2="score                         fuel      "

; THIS ARRAY REPRESENTING THE ENTIRE MEMORY
; USE THIS INSTEAD OF PEEK AND POKE
BYTE ARRAY  MATRIX=0

; OFFSET=ROW+4
CARD ARRAY OFFSET=[$150 $120 $F0 $C0 $90 $60 $30]

; MEMORY MAP
BYTE
  SDMCTL=$22F,CHBAS=$2F4,CH=$2FC,
  RANDOM=$D20A,HSCROL=$D404,WSYNC=$D40A,NMIEN=$D40E

; CUSTOM
BYTE
  ROW,NEXTROW=[0],TILE=[0],
  HSCROLI=$CA,
  I0=$CB,I1=$CC

; MEMORY MAP
CARD
  VDSLST=$200,
  SDLSTL=$230,
  VVBLKD=$224

; CUSTOM
CARD
  OLDVBL,
  LMS=$1007,
  SCREENI=[STARTROW],
  CI0=$CD

;------------------------------------------------------------------------------
; INCLUDES
;------------------------------------------------------------------------------

INCLUDE "H6:TOOLS.ACT"
INCLUDE "H6:TERRAIN.ACT"
INCLUDE "H6:VBLANKD.ACT"

;------------------------------------------------------------------------------
; INITIATION OF GLOBAL VARIABLES
;------------------------------------------------------------------------------

PROC INIT()
  SETBLOCK(STARTSCREEN,$FFF,00)

  OLDVBL=VVBLKD
  NEXTROW=0 TILE=0 SCREENI=STARTROW HSCROLI=3

  LOAD("H1:SCRAMBLE.FNT",STARTSCREEN+$800,$400,4)
  CHBAS=(STARTSCREEN+$800) RSH 8

  FOR I0=0 TO $2F DO
    SCREEN(I0+STARTROW+OFFSET(1))=GLADE
  OD

  MATRIX(COLOR1)=$E
  MATRIX(COLOR2)=0
  MATRIX(COLOR4)=2

  LMS=STARTSCREEN
  SDLSTL=DL
  VDSLST=DLI
  NMIEN=$C0
RETURN

;------------------------------------------------------------------------------
; LAUNCH PROCEDURE
;------------------------------------------------------------------------------

PROC MAIN()
  SDMCTL=0 INIT() SDMCTL=$22
  SETVBLK(7,(VBLANKD RSH 8),VBLANKD)

  WHILE CH#$1C DO OD ; press ESC

  SETVBLK(7,(OLDVBL RSH 8),OLDVBL)
  GRAPHICS(0)
RETURN
