SET $E=$2000
SET $491=$2000

;------------------------------------------------------------------------------

DEFINE
  RTI="$40",PHA="$48",PLA="$68",JMPI="$6C",STA="$8D",LDAI="$A9",DEC="$CE",CPMI="$C9",BNE="$D0"

DEFINE
  COLOR0="$2C4",COLOR1="$2C5",COLOR2="$2C6",COLOR3="$2C7",COLOR4="$2C8",
  CHBAS="$2F4",SETVBV="$E45C",NMIEN="$D40E"

DEFINE
  RFILL="14",LFILL="15",LSLOPE="70",RSLOPE="71",GLADE="77",
  START="$4000",STARTROW="$210",LOOP="$200"

;------------------------------------------------------------------------------

BYTE ARRAY
  DL=[
    $70
    $42$44$20
    $70$F0
    $D2$00$40 $92$92$92$92$92$12$12$12$12$12$12$12$12$12$12$12$12$12
    $70$70
    $42$6F$20
    $41$00$20
  ],
  DLI=[
    PHA
    LDAI 0 STA $0A $D4 STA $18 $D0
    DEC $27 $20 DEC $27 $20
    CPMI $80 BNE 5 LDAI 0 STA $18 $D0
    PLA
    RTI
  ]

BYTE ARRAY
  SCREEN=START,
  TEXT1="             ascii scramble             ",
  TEXT2="score                         fuel      ",
  MATRIX=0

  ; OFFSET+4
  CARD ARRAY OFFSET=[$150 $120 $F0 $C0 $90 $60 $30]

BYTE
  CH=$2FC,
  HSCROL=$D404,
  WSYNC=$D40A,
  RANDOM=$D20A,
  RTCLOCK=$14

BYTE
  ROW,NROW=[0],
  HSCROLI=$CA,
  I0=$CB,I1=$CC,I3=$CD,
  TILE=[0]

CARD
  VDSLST=$200,
  SDLSTL=$230,
  VVBLKD=$224

CARD
  OLDVBL,
  LMS=$2007,
  SCREENI=[STARTROW],
  CI0=$CE

;------------------------------------------------------------------------------

PROC WAIT=*(BYTE F)[$18$65$14$C5$14$D0$FC$60]

;------------------------------------------------------------------------------

PROC SETVBLK=SETVBV(BYTE WHICH,MSB,LSB)

;------------------------------------------------------------------------------

;M=4 LOAD, M=8 SAVE
PROC LOAD(CARD FILE,SADDR,NBYTES, BYTE M)
  CLOSE(2) OPEN(2,FILE,M,128)
  IF M=4 THEN POKE($362,7)
  ELSE POKE($362,11) FI
  POKEC($364,SADDR) POKEC($368,NBYTES)
  [$A2$20$20$56$E4$85$A3] CLOSE(2)
RETURN

;------------------------------------------------------------------------------

PROC TERRAIN()
  BYTE SHIP

  SCREEN(SCREENI+$180)=0
  SCREEN(SCREENI+OFFSET(0))=0
  SCREEN(SCREENI+OFFSET(1))=0
  SCREEN(SCREENI+OFFSET(2))=0
  SCREEN(SCREENI+OFFSET(3))=0
  SCREEN(SCREENI+OFFSET(4))=0
  SCREEN(SCREENI+OFFSET(5))=0
  SCREEN(SCREENI+OFFSET(6))=0
  SCREEN(SCREENI)=0

  TILE=RANDOM & 3
  ROW=NROW

  IF (ROW>0) AND (ROW<6) THEN
    IF TILE<2 THEN
      SCREEN(SCREENI+OFFSET(ROW))=GLADE
      IF ROW<5 THEN
        SHIP=RANDOM & 13
        IF SHIP=1 THEN
          SCREEN(SCREENI+OFFSET(ROW+1))=2
          SCREEN(SCREENI+OFFSET(ROW+2))=12
        ELSEIF SHIP=3 THEN
          SCREEN(SCREENI+OFFSET(ROW+1))=3
          SCREEN(SCREENI+OFFSET(ROW+2))=11
        ELSEIF SHIP=5 THEN
          SCREEN(SCREENI+OFFSET(ROW+1))=2
          SCREEN(SCREENI+OFFSET(ROW+2))=11
        ELSEIF SHIP=7 THEN
          SCREEN(SCREENI+OFFSET(ROW+1))=4
          SCREEN(SCREENI+OFFSET(ROW+2))=13
        ELSEIF SHIP=9 THEN
          SCREEN(SCREENI+OFFSET(ROW+1))=3
          SCREEN(SCREENI+OFFSET(ROW+2))=13
        FI
      FI
      RETURN
    ELSEIF TILE=2 THEN
      NROW=ROW-1
      SCREEN(SCREENI+OFFSET(NROW))=RFILL
      SCREEN(SCREENI+OFFSET(ROW))=RSLOPE
      RETURN
    ELSEIF TILE=3 THEN
      NROW=ROW+1
      SCREEN(SCREENI+OFFSET(ROW))=LFILL
      SCREEN(SCREENI+OFFSET(NROW))=LSLOPE
      RETURN
    FI
  FI

  IF ROW=6 THEN
    IF TILE=3 THEN
      SCREEN(SCREENI+OFFSET(6))=GLADE
      RETURN
    ELSE
      SCREEN(SCREENI+OFFSET(5))=RFILL
      SCREEN(SCREENI+OFFSET(6))=RSLOPE NROW=5
      RETURN
    FI
  FI

  IF ROW=0 AND TILE<2 THEN
    SCREEN(SCREENI+OFFSET(0))=GLADE
    RETURN
  ELSEIF ROW=0 THEN
    SCREEN(SCREENI+OFFSET(0))=LFILL
    SCREEN(SCREENI+OFFSET(1))=LSLOPE NROW=1
    RETURN
  FI
RETURN

;------------------------------------------------------------------------------

PROC VBLANKD()
  DLI(2)=$8C
  VDSLST=DLI
  IF HSCROLI=$FF THEN
    TERRAIN()
    IF LMS=START+LOOP THEN
      FOR I0=0 TO $2F DO
        SCREEN(I0+STARTROW+OFFSET(1))=GLADE
      OD
      NROW=0 SCREENI=STARTROW LMS=$4000
    ELSE
      LMS==+1 SCREENI==+1
    FI
    HSCROLI=3
  FI
  HSCROL=HSCROLI HSCROLI==-1
[JMPI OLDVBL]

;------------------------------------------------------------------------------

PROC INIT()
  SETBLOCK($4000,$FFF,00)

  OLDVBL=VVBLKD
  NROW=0 TILE=0 SCREENI=STARTROW HSCROLI=3

  LOAD("H1:SCRAMBLE.FNT",$1000,$400,4)
  MATRIX(CHBAS)=$10

  FOR I0=0 TO $2F DO
    SCREEN(I0+STARTROW+OFFSET(1))=GLADE
  OD

  MATRIX(COLOR1)=$E
  MATRIX(COLOR2)=0
  MATRIX(COLOR4)=2

  SDLSTL=DL
  VDSLST=DLI
  MATRIX(NMIEN)=$C0
RETURN

;------------------------------------------------------------------------------

PROC MAIN()
  INIT()

  SETVBLK(7,(VBLANKD RSH 8),VBLANKD)

  WHILE CH#$1C DO OD ; press ESC

  SETVBLK(7,(OLDVBL RSH 8),OLDVBL)

  GRAPHICS(0)
RETURN
