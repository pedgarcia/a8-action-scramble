SET $E=$2000
SET $491=$2000

;------------------------------------------------------------------------------

DEFINE
  RTI="$40",PHA="$48",PLA="$68",JMPI="$6C",STA="$8D",LDAI="$A9",
  CMPI="$C9",DEC="$CE",BNE="$D0"

DEFINE
  COLOR0="$2C4",COLOR1="$2C5",COLOR2="$2C6",COLOR3="$2C7",COLOR4="$2C8",
  COLPF0="$D016",COLPF1="$D017",COLPF2="$D018",COLPF3="$D019",COLBK="$D01A",
  SETVBV="$E45C"

DEFINE
  RFILL="14",LFILL="15",LSLOPE="70",RSLOPE="71",GLADE="77",
  START="$4000",STARTROW="$210",LOOP="$200"

;------------------------------------------------------------------------------

BYTE ARRAY
  DL=[
    $70
    $42 $49 $20
    $70 $F0
    $D2 $00 $40
    $92 $92 $92 $92 $92 $12 $12 $12 $12 $12 $12 $12 $12 $12 $12 $12 $12 $12
    $70 $70
    $42 $74 $20
    $41 $00 $20
  ],
  DLI=[
    PHA
    LDAI $8C STA $0A $D4 STA $18 $D0 DEC $27 $20 DEC $27 $20
    CMPI $80 BNE $A LDAI 0 STA $18 $D0 LDAI $8C STA $27 $20
    PLA RTI
  ]

BYTE ARRAY
  SCREEN=START,
  TEXT1="             ascii scramble             ",
  TEXT2="score                         fuel      ",
  MATRIX=0

; OFFSET+4
CARD ARRAY OFFSET=[$150 $120 $F0 $C0 $90 $60 $30]

BYTE
  SDMCTL=$22F,CHBAS=$2F4,CH=$2FC,
  RANDOM=$D20A,HSCROL=$D404,WSYNC=$D40A,NMIEN=$D40E

BYTE
  ROW,NROW=[0],TILE=[0],
  HSCROLI=$CA,
  I0=$CB,I1=$CC,I3=$CD,I4=$CE

CARD
  VDSLST=$200,
  SDLSTL=$230,
  VVBLKD=$224

CARD
  OLDVBL,
  LMS=$2007,
  SCREENI=[STARTROW]

;------------------------------------------------------------------------------

INCLUDE "H6:TOOLS.ACT"

;------------------------------------------------------------------------------

INCLUDE "H6:TERRAIN.ACT"

;------------------------------------------------------------------------------

INCLUDE "H6:VBLANKD.ACT"

;------------------------------------------------------------------------------

PROC INIT()
  SETBLOCK($4000,$FFF,00)

  OLDVBL=VVBLKD
  NROW=0 TILE=0 SCREENI=STARTROW HSCROLI=3

  LOAD("H1:SCRAMBLE.FNT",$1000,$400,4)
  CHBAS=$10

  FOR I0=0 TO $2F DO
    SCREEN(I0+STARTROW+OFFSET(1))=GLADE
  OD

  MATRIX(COLOR1)=$E
  MATRIX(COLOR2)=0
  MATRIX(COLOR4)=2

  SDLSTL=DL
  VDSLST=DLI
  NMIEN=$C0
RETURN

;------------------------------------------------------------------------------

PROC MAIN()
  SDMCTL=0 INIT() SDMCTL=$22
  SETVBLK(7,(VBLANKD RSH 8),VBLANKD)

  WHILE CH#$1C DO OD ; press ESC

  SETVBLK(7,(OLDVBL RSH 8),OLDVBL)
  GRAPHICS(0)
RETURN
