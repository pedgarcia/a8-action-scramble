SET $E=$2000
SET $491=$2000

;------------------------------------------------------------------------------

DEFINE
  COLOR0="$2C4",COLOR1="$2C5",COLOR2="$2C6",COLOR3="$2C7",COLOR4="$2C8",
  CHBAS="$2F4",
  RFILL="14",LFILL="15",LSLOPE="70",RSLOPE="71",GLADE="77",
  STARTR="528",LOOP="1024"

;------------------------------------------------------------------------------

BYTE ARRAY
  DL=[
    $70
    $46$28$20
    $70$70
    $52$00$40 $12$12$12$12$12$12$12$12$12$12$12$12$12$12$12$12$12$12
    $70$70
    $46$3F$20
    $41$00$20
  ],
  SCREEN=$4000,
  TEXT_1="@@@ascii@scramble@@@",
  TEXT_2="scoreZ@@@@@livesZ@@@",
  MATRIX=0

CARD ARRAY OFFSET=[332 284 236 188 140 92 44]

BYTE
  CH=$2FC,
  HSCROL=$D404,
  WSYNC=$D40A,
  RANDOM=$D20A,
  ROW,NROW=[0],
  TILE=[0]
CARD
  SDLSTL=$230,
  LMS=$2007,
  CI=[STARTR]

;------------------------------------------------------------------------------

PROC WAIT=*(BYTE F)[$18$65$14$C5$14$D0$FC$60]

;------------------------------------------------------------------------------

;M=4 LOAD, M=8 SAVE
PROC LOAD(CARD FILE,SADDR,NBYTES, BYTE M)
  CLOSE(2) OPEN(2,FILE,M,128)
  IF M=4 THEN POKE($362,7)
  ELSE POKE($362,11) FI
  POKEC($364,SADDR) POKEC($368,NBYTES)
  [$A2$20$20$56$E4$85$A3] CLOSE(2)
RETURN

;------------------------------------------------------------------------------

PROC TERRAIN()
  BYTE SHIP

  SCREEN(CI+380)=0
  SCREEN(CI+OFFSET(0))=0
  SCREEN(CI+OFFSET(1))=0
  SCREEN(CI+OFFSET(2))=0
  SCREEN(CI+OFFSET(3))=0
  SCREEN(CI+OFFSET(4))=0
  SCREEN(CI+OFFSET(5))=0
  SCREEN(CI+OFFSET(6))=0
  SCREEN(CI)=0

  TILE=RANDOM & 3
  ROW=NROW

  IF (ROW>0) AND (ROW<6) THEN
    IF TILE<2 THEN
      SCREEN(CI+OFFSET(ROW))=GLADE
      IF ROW<5 THEN
        SHIP=RANDOM & 13
        IF SHIP=1 THEN
          SCREEN(CI+OFFSET(ROW+1))=2
          SCREEN(CI+OFFSET(ROW+2))=12
        ELSEIF SHIP=3 THEN
          SCREEN(CI+OFFSET(ROW+1))=3
          SCREEN(CI+OFFSET(ROW+2))=11
        ELSEIF SHIP=5 THEN
          SCREEN(CI+OFFSET(ROW+1))=2
          SCREEN(CI+OFFSET(ROW+2))=11
        ELSEIF SHIP=7 THEN
          SCREEN(CI+OFFSET(ROW+1))=4
          SCREEN(CI+OFFSET(ROW+2))=13
        ELSEIF SHIP=9 THEN
          SCREEN(CI+OFFSET(ROW+1))=3
          SCREEN(CI+OFFSET(ROW+2))=13
        FI
      FI
      RETURN
    ELSEIF TILE=2 THEN
      NROW=ROW-1
      SCREEN(CI+OFFSET(NROW))=RFILL
      SCREEN(CI+OFFSET(ROW))=RSLOPE
      RETURN
    ELSEIF TILE=3 THEN
      NROW=ROW+1
      SCREEN(CI+OFFSET(ROW))=LFILL
      SCREEN(CI+OFFSET(NROW))=LSLOPE
      RETURN
    FI
  FI

  IF ROW=6 THEN
    IF TILE=3 THEN
      SCREEN(CI+OFFSET(6))=GLADE
      RETURN
    ELSE
      SCREEN(CI+OFFSET(5))=RFILL
      SCREEN(CI+OFFSET(6))=RSLOPE NROW=5
      RETURN
    FI
  FI

  IF ROW=0 AND TILE<2 THEN
    SCREEN(CI+OFFSET(0))=GLADE
    RETURN
  ELSEIF ROW=0 THEN
    SCREEN(CI+OFFSET(0))=LFILL
    SCREEN(CI+OFFSET(1))=LSLOPE NROW=1
    RETURN
  FI
RETURN

;------------------------------------------------------------------------------

PROC MAIN()
  BYTE I=$CA

  SETBLOCK($4000,$FFF,00)

  LOAD("H1:SCRAMBLE.FNT",$5000,$400,4)
  MATRIX(CHBAS)=$50

  FOR I=0 TO 47 DO
    SCREEN(I+STARTR+OFFSET(1))=GLADE
  OD

  SDLSTL=DL

  WHILE CH#28 DO
    TERRAIN()

    I=4
    WHILE I DO
      I==-1
      HSCROL=I
      WAIT(1)
    OD

    LMS==+1
    CI==+1

    IF CI=STARTR+LOOP THEN
      PUT(253)
      FOR I=0 TO 47 DO
        SCREEN(I+STARTR+OFFSET(1))=GLADE
      OD
      NROW=0 CI=STARTR LMS=$4000
    FI
  OD

  GRAPHICS(0)
RETURN
