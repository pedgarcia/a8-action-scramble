;------------------------------------------------------------------------------
; Action! 3.7P BY JAC! https://atariwiki.org/wiki/Wiki.jsp?page=Action
;------------------------------------------------------------------------------
; ATARI MEMORY MAP     https://www.atariarchives.org/mapping/memorymap.php
;------------------------------------------------------------------------------
; Atari800             https://atari800.github.io
; Graph2Font           http://g2f.atari8.info/instrukcja.html
; Atari FontMaker      https://sourceforge.net/projects/atari-fontmaker
; Sprite Editor XL     http://bocianu.atari.pl/blog/spriteXL
; Exomizer             https://bitbucket.org/magli143/exomizer/wiki/Home
;------------------------------------------------------------------------------
; Sublime Text 3       https://www.sublimetext.com/3
; Syntax highliting scheme for Action! ATARI language
; https://github.com/michalkolodziejski/ST3-action-syntax-highlighting
;------------------------------------------------------------------------------

MODULE

;------------------------------------------------------------------------------
; CODEBASE START
;------------------------------------------------------------------------------

SET $E=$1000
SET $491=$1000

;------------------------------------------------------------------------------
; DEFINED CONSTANTS
;------------------------------------------------------------------------------

; MSB OF SET CODEBASE
DEFINE CBMSB="$10"

; ML OPCODES AND SNIPPETS
DEFINE
  RTI="$40",PHA="$48",PLA="$68",JMPI="$6C",STA="$8D",LDAI="$A9",
  CMPI="$C9",DEC="$CE",BNE="$D0", WSYNC="$8D$0A$D4"

; MEMORY MAP
DEFINE
  COLOR0="$2C4",COLOR1="$2C5",COLOR2="$2C6",COLOR3="$2C7",COLOR4="$2C8",
  COLPF0="$D016",COLPF1="$D017",COLPF2="$D018",COLPF3="$D019",COLBK="$D01A",
  SETVBV="$E45C"

; CUSTOM
DEFINE
  RFILL="14",LFILL="15",LSLOPE="70",RSLOPE="71",GLADE="77",
  STARTSCREEN="$2000",STARTROW="$2A0",LOOP="$200",CHARSETREL="$800"

;------------------------------------------------------------------------------
; GLOBAL VARIABLES
;------------------------------------------------------------------------------

BYTE ARRAY
  DL=[
    $70           ; EMPTY ROW
    $42 $4C CBMSB ; FIRST TEXT ROW (GR.0)
    $70 $F0       ; TWO EMPTY ROWS, LAST WITH DLI
    $D2 0 0       ; STARTSCREEN (GR.0) WITH HSCROL AND DLI
    $92 $92 $92 $92 $92 $12 $12 $12 $12 $12 $12 $12 $12 $12 $12 $12 $12 $12 $12 $12 $12
    $70 $70       ; TWO EMPTY ROWS
    $42 $77 CBMSB ; SECOND TEXT ROW (GR.0)
    $41 $00 CBMSB ; JUMP TO BEGINING OF DL
  ],
  DLI=[
    WSYNC PHA
    LDAI $8C STA $18 $D0 DEC $2D CBMSB DEC $2D CBMSB
    CMPI $80 BNE $A LDAI 0 STA $18 $D0 LDAI $8C STA $2D CBMSB
    PLA RTI
  ]

BYTE ARRAY
  SCREEN=STARTSCREEN,
  TEXT1="             ascii scramble             ",
  TEXT2="score                         fuel      "

; THIS ARRAY REPRESENTING THE ENTIRE MEMORY
; USE THIS INSTEAD OF PEEK AND POKE
BYTE ARRAY  MATRIX=0

; ROWOFFSET=ROW+4
CARD ARRAY ROWOFFSET=[$150 $120 $F0 $C0 $90 $60 $30]

; MEMORY MAP
BYTE
  SDMCTL=$22F,CHBAS=$2F4,CH=$2FC,
  RANDOM=$D20A,HSCROL=$D404,NMIEN=$D40E

; CUSTOM
BYTE
  ROW,NEXTROW=[0],TILE=[0],
  HSCROLI=$55, ; REUSE COLCRS
  I0=$CA,I1=$CB

; MEMORY MAP
CARD
  VDSLST=$200,
  SDLSTL=$230,
  VVBLKD=$224

; CUSTOM
CARD
  OLDVBL,
  LMS=$1007,
  SCREENI=[STARTROW],
  CI0=$CC

;------------------------------------------------------------------------------
; INCLUDES
;------------------------------------------------------------------------------

INCLUDE "H6:RUNTIME.ACT"
INCLUDE "H6:TOOLS.ACT"
INCLUDE "H6:TERRAIN.ACT"
INCLUDE "H6:VBLANKD.ACT"

;------------------------------------------------------------------------------
; INITIALIZATION OF GLOBAL VARIABLES
;------------------------------------------------------------------------------

PROC INIT()
  CARD NEWCHBAS

  OLDVBL=VVBLKD
  NEWCHBAS=STARTSCREEN+CHARSETREL
  NEXTROW=0 TILE=0 SCREENI=STARTROW HSCROLI=3

  LOAD("H1:SCRAMBLE.FNT",STARTSCREEN+CHARSETREL,$400,4)
  CHBAS=MATRIX(@NEWCHBAS+1)

  FOR I0=0 TO $2F DO
    SCREEN(I0+STARTROW+ROWOFFSET(1))=GLADE
  OD

  MATRIX(COLOR1)=$E
  MATRIX(COLOR2)=0
  MATRIX(COLOR4)=2

  LMS=STARTSCREEN
  SDLSTL=DL
  VDSLST=DLI
  NMIEN=$C0
RETURN

;------------------------------------------------------------------------------
; LAUNCH PROCEDURE
;------------------------------------------------------------------------------

PROC MAIN()
  CARD VBLANKDADDR

  SDMCTL=0    ; TURN OFF ANTIC
  INIT()
  VBLANKDADDR=VBLANKD
  SETVBLK(7,MATRIX(@VBLANKDADDR+1),MATRIX(@VBLANKDADDR))
  SDMCTL=$22  ; TURN ON ANTIC

  ; MAIN LOOP
  WHILE CH#$1C DO

    IF HSCROLI=$FF THEN
      TERRAIN()
      IF LMS=STARTSCREEN+LOOP THEN
        FOR I0=0 TO $2F DO
          SCREEN(I0+STARTROW+ROWOFFSET(1))=GLADE
        OD
        NEXTROW=0 SCREENI=STARTROW LMS=STARTSCREEN
      ELSE
        LMS==+1 SCREENI==+1
      FI
      HSCROLI=3
    FI

    HSCROL=HSCROLI HSCROLI==-1

    WAIT(1)
  OD ; press ESC]

  SETVBLK(7,MATRIX(@OLDVBL+1),MATRIX(@OLDVBL))

  GRAPHICS(0)
RETURN
