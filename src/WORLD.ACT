;------------------------------------------------------------------------------
; WORLD GENERATION PROCEDURE
;------------------------------------------------------------------------------

;------------------------------------------------------------------------------
; PROCEDURE DRAWS ROCKETS
;------------------------------------------------------------------------------
PROC DRAWROCKET=*()
  SHIPTYPE=RANDOM & 30
  IF SHIPTYPE>8 THEN RET FI
  ROCKETBODY=SHIPSTABLE(SHIPTYPE)
  ROCKETHEAD=SHIPSTABLE(SHIPTYPE+1)
RET

;------------------------------------------------------------------------------
; PROCEDURE DRAWS ION CANNON STRUCTURE
;------------------------------------------------------------------------------
PROC DRAWCANNON=*()
  CTMP2=SCREENI+ROWOFFSET(ROW+2) ; 2 FLOOR
  CTMP1=CTMP2+$30                ; 1 FLOOR

  SCREEN(CTMP1-2)=27
  SCREEN(CTMP2-2)=0
  SCREEN(CTMP1-1)=28
  SCREEN(CTMP2-1)=0
  SCREEN(CTMP1)=29
  SCREEN(CTMP2)=30
RET

;------------------------------------------------------------------------------
; TERRAIN GENERATION PROCEDURE
;------------------------------------------------------------------------------
PROC TERRAIN=*()
  CTMP2=STARTSCREEN+SCREENI
; 1126: 18        CLC
; 1127: A9 00     LDA #$00
; 1129: 65 5E     ADC $5E
; 112B: 85 CC     STA $CC
; 112D: A9 80     LDA #$80
; 112F: 65 5F     ADC $5F
; 1131: 85 CD     STA $CD
  [
    $A2$07$A9$00$A8$91$CC$18
    $A5$CC$69$30$85$CC$90$02
    $E6$CD$CA$10$ED
  ]
; 1133: A2 07     LDX #$07
; 1135: A9 00     LDA #$00
; 1137: A8        TAY
; 1138: 91 CC     STA ($CC),Y
; 113A: 18        CLC
; 113B: A5 CC     LDA $CC
; 113D: 69 30     ADC #$30
; 113F: 85 CC     STA $CC
; 1141: 90 02     BCC $1145
; 1143: E6 CD     INC $CD
; 1145: CA        DEX
; 1146: 10 ED     BPL $1135

  ROW=NEXTROW

  ; LEVELING THE AREA
  IF LMS>STARTSCREEN+LOOP-$30 THEN
    IF ROW>0 THEN
      NEXTROW==-1
      DRAWROW=RSLOPE DRAWNEXTROW=RFILL
      RET
    ELSE
      DRAWROW=GLADE RET
    FI
  FI

  TILE=RANDOM & 3

  IF (ROW>0) AND (ROW<6) THEN
    IF TILE<2 THEN
      DRAWROW=GLADE
      IF ROW<5 THEN

        ; SETS UP BUILDINGS AND VEHICLES
        ISCANONN==+1
        IF ISCANONN>2 THEN
          IF (RANDOM&3)=0 THEN
            DRAWCANNON() ISCANONN=0 RET
          FI
        FI
        DRAWROCKET() RET

      FI
    ELSEIF TILE=2 THEN
      ISCANONN=0 NEXTROW==-1
      DRAWROW=RSLOPE DRAWNEXTROW=RFILL
      RET
    ELSEIF TILE=3 THEN
      ISCANONN=0 NEXTROW==+1
      DRAWROW=LFILL DRAWNEXTROW=LSLOPE
      RET
    FI
  FI

  IF ROW=0 THEN
    IF TILE<2 THEN
      DRAWROW=GLADE RET
    ELSE
      NEXTROW=1
      DRAWROW=LFILL DRAWNEXTROW=LSLOPE
      RET
    FI
  FI

  IF ROW=6 THEN
    IF TILE=3 THEN
      DRAWROW=GLADE RET
    ELSE
      NEXTROW=5
      DRAWROW=RSLOPE DRAWNEXTROW=RFILL
      RET
    FI
  FI
RET

;------------------------------------------------------------------------------
; PROCEDURE DRAWS LOOP LINE
;------------------------------------------------------------------------------
PROC DRAWLINE=*()
  SETBLOCK(STARTSCREEN+STARTROW+$120,$30,GLADE)
RET
