;------------------------------------------------------------------------------
; WORLD GENERATION PROCEDURE
;------------------------------------------------------------------------------

;------------------------------------------------------------------------------
; PROCEDURE DRAWS ROCKETS
;------------------------------------------------------------------------------
PROC DRAWROCKET=*()
  SHIPTYPE=RANDOM & 30
  IF SHIPTYPE>8 THEN RET FI
  ROCKETBODY=SHIPSTABLE(SHIPTYPE)
  ROCKETHEAD=SHIPSTABLE(SHIPTYPE+1)
RET

;------------------------------------------------------------------------------
; PROCEDURE DRAWS ION CANNON STRUCTURE
;------------------------------------------------------------------------------
PROC DRAWCANNON=*()
  CTMP0=SCREENI+ROWOFFSET(ROW+1)
  CTMP1=SCREENI+ROWOFFSET(ROW+2)

  SCREEN(CTMP0-2)=27
  SCREEN(CTMP1-2)=0
  SCREEN(CTMP0-1)=28
  SCREEN(CTMP1-1)=0
  SCREEN(CTMP0)=29
  SCREEN(CTMP1)=30
RET

;------------------------------------------------------------------------------
; TERRAIN GENERATION PROCEDURE
;------------------------------------------------------------------------------
PROC TERRAIN=*()
  SCREEN(SCREENI+$150)=0
  SCREEN(SCREENI+$120)=0
  SCREEN(SCREENI+$F0)=0
  SCREEN(SCREENI+$C0)=0
  SCREEN(SCREENI+$90)=0
  SCREEN(SCREENI+$60)=0
  SCREEN(SCREENI+$30)=0
  SCREEN(SCREENI)=0

  ROW=NEXTROW

  IF LMS>STARTSCREEN+LOOP-$30 THEN
    IF ROW>0 THEN
      NEXTROW==-1
      DRAWROW=RSLOPE DRAWNEXTROW=RFILL
      RET
    ELSE
      DRAWROW=GLADE RET
    FI
  FI

  TILE=RANDOM & 3

  IF (ROW>0) AND (ROW<6) THEN
    IF TILE<2 THEN
      DRAWROW=GLADE
      IF ROW<5 THEN
        ISCANONN==+1
        IF ISCANONN>2 THEN
          IF (RANDOM&3)=0 THEN
            DRAWCANNON() ISCANONN=0 RET
          FI
        FI
        DRAWROCKET() RET
      FI
    ELSEIF TILE=2 THEN
      ISCANONN=0 NEXTROW==-1
      DRAWROW=RSLOPE DRAWNEXTROW=RFILL
      RET
    ELSEIF TILE=3 THEN
      ISCANONN=0 NEXTROW==+1
      DRAWROW=LFILL DRAWNEXTROW=LSLOPE
      RET
    FI
  FI

  IF ROW=0 THEN
    IF TILE<2 THEN
      DRAWROW=GLADE RET
    ELSE
      NEXTROW=1
      DRAWROW=LFILL DRAWNEXTROW=LSLOPE
      RET
    FI
  FI

  IF ROW=6 THEN
    IF TILE=3 THEN
      DRAWROW=GLADE RET
    ELSE
      NEXTROW=5
      DRAWROW=RSLOPE DRAWNEXTROW=RFILL
      RET
    FI
  FI
RET

;------------------------------------------------------------------------------
; PROCEDURE DRAWS LOOP LINE
;------------------------------------------------------------------------------
PROC DRAWLINE=*()
  SETBLOCK(STARTSCREEN+STARTROW+$120,$30,GLADE)
RET
