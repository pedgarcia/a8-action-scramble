;------------------------------------------------------------------------------
; WORLD GENERATION PROCEDURE
;------------------------------------------------------------------------------

;------------------------------------------------------------------------------
; PROCEDURE DRAWS ROCKETS
;------------------------------------------------------------------------------
PROC DRAWROCKET=*()
  SHIPTYPE=RANDOM & 15
  IF SHIPTYPE>4 THEN RET FI
  POKE(SCREENI-$30,SHIPSBODY(SHIPTYPE))
  POKE(SCREENI-$60,SHIPSHEAD(SHIPTYPE))
RET

;------------------------------------------------------------------------------
; PROCEDURE DRAWS ION CANNON STRUCTURE
;------------------------------------------------------------------------------
PROC DRAWCANNON=*()
  POKEC(SCREENI-$32,$1C1B)
  POKEC(SCREENI-$62,0)
  POKE(SCREENI-$30,29)
  POKE(SCREENI-$60,30)
RET

;------------------------------------------------------------------------------
; TERRAIN GENERATION PROCEDURE
;------------------------------------------------------------------------------
PROC TERRAIN=*()

  ;--------------------------------!
  ; CLEAR RIGHT COLUMN             !
  ;--------------------------------!
  CTMP2=LMS+STARTROW               ;
  [                                ;
    $A2$07$A0$00$98$91$CC$18       ;
    $A5$CC$69$30$85$CC$90$02       ;
    $E6$CD$CA$10$EF                ;
  ]                                ;
  ; 1133: A2 07     LDX #$07       !
  ; 1135: A0 00     LDY #$00       !
  ; 1137: 98        TYA            !
  ; 1138: 91 CC     STA ($CC),Y    !
  ; 113A: 18        CLC            !
  ; 113B: A5 CC     LDA $CC        !
  ; 113D: 69 30     ADC #$30       !
  ; 113F: 85 CC     STA $CC        !
  ; 1141: 90 02     BCC $1145      !
  ; 1143: E6 CD     INC $CD        !
  ; 1145: CA        DEX            !
  ; 1146: 10 EF     BPL $1137      !
  ;--------------------------------!

  ; LEVELING THE AREA
  IF LMS>STARTSCREEN+LOOP-$30 THEN
    IF ROW>0 THEN
      ROW==-1
      POKE(SCREENI,RSLOPE)
      SCREENI==+$30
      POKE(SCREENI,RFILL)
      RET
    ELSE
      POKE(SCREENI,GLADE) RET
    FI
  FI

  TILE=RANDOM & 3

  IF (ROW>0) AND (ROW<6) THEN
    IF TILE<2 THEN
      POKE(SCREENI,GLADE)
      IF ROW<5 THEN

        ; SETS UP BUILDINGS AND VEHICLES
        ISCANONN==+1
        IF ISCANONN>2 THEN
          IF (RANDOM&3)=0 THEN
            DRAWCANNON() ISCANONN=0 RET
          FI
        FI
        DRAWROCKET() RET

      FI
    ELSEIF TILE=2 THEN
      ISCANONN=0 ROW==-1
      POKE(SCREENI,RSLOPE)
      SCREENI==+$30
      POKE(SCREENI,RFILL)
      RET
    ELSEIF TILE=3 THEN
      ISCANONN=0 ROW==+1
      POKE(SCREENI,LFILL)
      SCREENI==-$30
      POKE(SCREENI,LSLOPE)
      RET
    FI
  FI

  IF ROW=0 THEN
    IF TILE<2 THEN
      POKE(SCREENI,GLADE) RET
    ELSE
      ROW=1
      POKE(SCREENI,LFILL)
      SCREENI==-$30
      POKE(SCREENI,LSLOPE)
      RET
    FI
  FI

  IF ROW=6 THEN
    IF TILE=3 THEN
      POKE(SCREENI,GLADE) RET
    ELSE
      ROW=5
      POKE(SCREENI,RSLOPE)
      SCREENI==+$30
      POKE(SCREENI,RFILL)
      RET
    FI
  FI
RET

;------------------------------------------------------------------------------
; PROCEDURE DRAWS LOOP LINE
;------------------------------------------------------------------------------
PROC DRAWLINE=*()
  SETBLOCK(STARTPLAIN,$30,GLADE)
RET
