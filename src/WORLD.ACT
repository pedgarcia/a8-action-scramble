;------------------------------------------------------------------------------
; WORLD GENERATION PROCEDURE
;------------------------------------------------------------------------------

;------------------------------------------------------------------------------
; PROCEDURE DRAWS BUILDINGS AND SPACE VEHICLES
;------------------------------------------------------------------------------

PROC DRAWSHIP=*()
  SHIPTYPE=RANDOM & 30
  IF SHIPTYPE>8 THEN RET FI
  ROCKETBODY=SHIPSTABLE(SHIPTYPE)
  ROCKETHEAD=SHIPSTABLE(SHIPTYPE+1)
RET

PROC DRAWRADAR()
  BYTE BTMP1,BTMP2
  CARD CTMP1,CTMP2

  CTMP1=SCREENI+ROWOFFSET(ROW+1)
  CTMP2=SCREENI+ROWOFFSET(ROW+2)
  BTMP1=SCREEN(SCREENI+ROWOFFSET(ROW)-2)
  BTMP2=SCREEN(SCREENI+ROWOFFSET(ROW)-1)
  IF BTMP1=GLADE AND BTMP2=GLADE THEN
    SCREEN(CTMP1-2)=27
    SCREEN(CTMP2-2)=0
    SCREEN(CTMP1-1)=28
    SCREEN(CTMP2-1)=0
    SCREEN(CTMP1)=29
    SCREEN(CTMP2)=30
  FI
RET

;------------------------------------------------------------------------------
; TERRAIN GENERATION PROCEDURE
;------------------------------------------------------------------------------

PROC TERRAIN=*()
  TERRAINI==+1

  SCREEN(SCREENI+$150)=0
  SCREEN(SCREENI+$120)=0
  SCREEN(SCREENI+$F0)=0
  SCREEN(SCREENI+$C0)=0
  SCREEN(SCREENI+$90)=0
  SCREEN(SCREENI+$60)=0
  SCREEN(SCREENI+$30)=0
  SCREEN(SCREENI)=0

  ROW=NEXTROW

  IF LMS>STARTSCREEN+LOOP-$30 THEN
    IF ROW>0 THEN
      NEXTROW==-1
      DRAWROW=RSLOPE DRAWNEXTROW=RFILL
      RET
    ELSE
      DRAWROW=GLADE RET
    FI
  FI

  TILE=RANDOM & 3

  IF (ROW>0) AND (ROW<6) THEN
    IF TILE<2 THEN
      DRAWROW=GLADE
      IF ROW<5 THEN
        DRAWSHIP()
        IF (RANDOM&1)=1 AND (TERRAINI&3)=3 THEN
          DRAWRADAR()
        FI
      FI
      RET
    ELSEIF TILE=2 THEN
      NEXTROW==-1
      DRAWROW=RSLOPE DRAWNEXTROW=RFILL
      RET
    ELSEIF TILE=3 THEN
      NEXTROW==+1
      DRAWROW=LFILL DRAWNEXTROW=LSLOPE
      RET
    FI
  FI

  IF ROW=0 THEN
    IF TILE<2 THEN
      DRAWROW=GLADE RET
    ELSE
      NEXTROW=1
      DRAWROW=LFILL DRAWNEXTROW=LSLOPE
      RET
    FI
  FI

  IF ROW=6 THEN
    IF TILE=3 THEN
      DRAWROW=GLADE RET
    ELSE
      NEXTROW=5
      DRAWROW=RSLOPE DRAWNEXTROW=RFILL
      RET
    FI
  FI
RET

;------------------------------------------------------------------------------
; PROCEDURE DRAWS LOOP LINE
;------------------------------------------------------------------------------

PROC DRAWLINE=*()
  SETBLOCK(STARTSCREEN+STARTROW+$120,$30,GLADE)
RET
