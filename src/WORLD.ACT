;------------------------------------------------------------------------------
; WORLD GENERATION PROCEDURE
;------------------------------------------------------------------------------

;------------------------------------------------------------------------------
; PROCEDURE DRAWS ROCKETS
;------------------------------------------------------------------------------
PROC DRAWROCKET=*()
  SHIPTYPE=(RANDOM%1) & 31
  IF SHIPTYPE>7 THEN RET FI
  POKEADDR=SCREENI-$30 ELMPOKE(SHIPTYPE) INC SHIPTYPE CNI
  POKEADDR=SCREENI-$60 ELMPOKE(SHIPTYPE)
RET

;------------------------------------------------------------------------------
; PROCEDURE DRAWS ION CANNON STRUCTURE
;------------------------------------------------------------------------------
PROC DRAWCANNON=*()
  POKEADDR=SCREENI-$32 ELMPOKEC($0A09)
  POKEADDR=SCREENI-$62 ELMPOKEC(0)
  POKEADDR=SCREENI-$30 ELMPOKE(11)
  POKEADDR=SCREENI-$60 ELMPOKE(12)
RET

;------------------------------------------------------------------------------
; TERRAIN GENERATION PROCEDURE
;------------------------------------------------------------------------------
PROC TERRAIN=*()

  ;--------------------------------!
  ; CLEAR RIGHT COLUMN             !
  ; LDX #$07 ROWS TO CLEAR         !
  ;--------------------------------!
  COLTOCLEAN=LMS+STARTROW          ;
  [                                ;
    $A2$07$A0$00$98$91$CC$18       ;
    $A5$CC$69$30$85$CC$90$02       ;
    $E6$CD$CA$10$EF                ;
  ]                                ;
  ; 1133: A2 07     LDX #$07       !
  ; 1135: A0 00     LDY #$00       !
  ; 1137: 98        TYA            !
  ; 1138: 91 CC     STA ($CC),Y    !
  ; 113A: 18        CLC            !
  ; 113B: A5 CC     LDA $CC        !
  ; 113D: 69 30     ADC #$30       !
  ; 113F: 85 CC     STA $CC        !
  ; 1141: 90 02     BCC $1145      !
  ; 1143: E6 CD     INC $CD        !
  ; 1145: CA        DEX            !
  ; 1146: 10 EF     BPL $1137      !
  ;--------------------------------!

  ; LEVELING THE AREA
  IF LMS>TERRAINEND THEN
    IF ROW>0 THEN
      DEC ROW CED
      TERPOKE(RSLOPE) SCREENI==+$30 TERPOKE(RFILL)
      RET
    ELSE
      TERPOKE(GLADE) RET
    FI
  FI

  TILE=RANDOM & 3

  IF ROW=0 THEN
    IF TILE<2 THEN
      TERPOKE(GLADE) RET
    ELSE
      ROW=1
      TERPOKE(LFILL) SCREENI==-$30 TERPOKE(LSLOPE)
      RET
    FI
  FI

  IF ROW=6 THEN
    IF TILE=3 THEN
      TERPOKE(GLADE) RET
    ELSE
      ROW=5
      TERPOKE(RSLOPE) SCREENI==+$30 TERPOKE(RFILL)
      RET
    FI
  FI

  IF TILE=2 THEN
    ISCANONN=0 DEC ROW CED
    TERPOKE(RSLOPE) SCREENI==+$30 TERPOKE(RFILL)
    RET
  ELSEIF TILE=3 THEN
    ISCANONN=0 INC ROW CNI
    TERPOKE(LFILL) SCREENI==-$30 TERPOKE(LSLOPE)
    RET
  FI

  TERPOKE(GLADE)

  IF ROW<5 THEN
    ; SETS UP BUILDINGS AND VEHICLES
    INC ISCANONN CNI
    IF ISCANONN>2 THEN
      IF (RANDOM&3)=0 THEN
        DRAWCANNON() ISCANONN=0
        RET
      FI
    FI
    DRAWROCKET()
  FI

RET

;------------------------------------------------------------------------------
; PROCEDURE DRAWS LOOP LINE
;------------------------------------------------------------------------------
PROC DRAWLINE=*()
  SETBLOCK(STARTPLAIN,$30,GLADE)
RET
