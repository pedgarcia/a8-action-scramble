;------------------------------------------------------------------------------
; Action! 3.6 by OSS   https://atariwiki.org/wiki/Wiki.jsp?page=Action
;------------------------------------------------------------------------------
; ATARI MEMORY MAP     https://www.atariarchives.org/mapping/memorymap.php
;------------------------------------------------------------------------------
; Atari800             https://atari800.github.io
; Graph2Font           http://g2f.atari8.info/instrukcja.html
; Atari FontMaker      https://sourceforge.net/projects/atari-fontmaker
; Sprite Editor XL     http://bocianu.atari.pl/blog/spriteXL
; Exomizer             https://bitbucket.org/magli143/exomizer/wiki/Home
;------------------------------------------------------------------------------
; Sublime Text 3       https://www.sublimetext.com/3
; Syntax highliting scheme for Action! ATARI language
; https://github.com/michalkolodziejski/ST3-action-syntax-highlighting
;------------------------------------------------------------------------------

;------------------------------------------------------------------------------
; CONSTANT-LIKE DEFINITIONS AMD CODE SNIPPETS
;------------------------------------------------------------------------------

; CODEBASE, CODEBASE MSB, LMS IN PLAYFIELD DISPLAY LIST AND OTHERS
DEFINE
  CODEBASE="$1000",CBMSB="$10",LMSADDR="$1005",
  STARTSCREEN="$9000",STARTROW="$360",LOOP="$300",CHARSETREL="$800"

; ML OPCODES
DEFINE
  CLC="$18",RTI="$40",PHA="$48",PLA="$68",ADCI="$69",JMPI="$6C",
  STA="$8D",LDAI="$A9",LDA="$AD",CMPI="$C9",DEC="$CE",BNE="$D0",INC="$EE",
  WSYNC="$8D$0A$D4"

; MEMORY MAP
DEFINE
  COLOR0="$2C4",COLOR1="$2C5",COLOR2="$2C6",COLOR3="$2C7",COLOR4="$2C8",
  COLPF0="$D016",COLPF1="$D017",COLPF2="$D018",COLPF3="$D019",COLBK="$D01A",
  SETVBV="$E45C"

; CUSTOM
DEFINE
  RFILL="14",LFILL="15",LSLOPE="70",RSLOPE="71",GLADE="77"

;------------------------------------------------------------------------------
; COMPILATOR DIRECTIVES
;------------------------------------------------------------------------------

SET $E=CODEBASE
SET $491=CODEBASE

;------------------------------------------------------------------------------
; MODULE CODE START HERE
;------------------------------------------------------------------------------

MODULE

;------------------------------------------------------------------------------
; GLOBAL VARIABLES
;------------------------------------------------------------------------------

INCLUDE "H6:ANTIC.ACT"
INCLUDE "H6:GLOBALS.ACT"

;------------------------------------------------------------------------------
; PROCEDURES AND FUNCTIONS
;------------------------------------------------------------------------------

INCLUDE "H6:TOOLS.ACT"
INCLUDE "H6:WORLD.ACT"
INCLUDE "H6:VBLANKD.ACT"
INCLUDE "H6:LOOP.ACT"

;------------------------------------------------------------------------------
; INITIALIZING THE PROGRAM VARIABLES AND INTERRUPTIONS
;------------------------------------------------------------------------------

SET $E=$4000

PROC INIT()
  CARD POINTER ADDR

  SDMCTL=0          ; TURN ON ANTIC

  SETBLOCK(STARTSCREEN,CHARSETREL+$400,0)

  POKE(COLOR1,$E)   ; WHITE CHAR / TERRAIN
  POKE(COLOR2,$8C)  ; BACKGROUND / SKY
  POKE(COLOR4,0)    ; BORDER

  NEXTROW=0 TILE=0 HSCROLI=3
  LMS=STARTSCREEN SCREENI=STARTROW

  SETBLOCK(TEXT,$30,0)
  ; SET TEXT LMS IN DISPLAY LIST
  ADDR=DL+2 ADDR^=TEXT

  ADDR=STARTSCREEN+CHARSETREL
  LOAD("H1:SCRAMBLE.FNT",ADDR,$400,4)
  CHBAS=PEEK(@ADDR+1)

  SDLSTL=DL
  VDSLST=DLIA

  ADDR=VBLANKD
  LSB=PEEK(@ADDR) MSB=PEEK(@ADDR+1)
  OLDVBL=VVBLKD SETVBLK(7,MSB,LSB)

  ; DLI VBI
  ;  1   1   000000 = $C0
  NMIEN=$C0

  SDMCTL=$22        ; TURN OFF ANTIC
RETURN

;------------------------------------------------------------------------------
; LAUNCH PROCEDURE
;------------------------------------------------------------------------------

PROC MAIN()
  INIT()
  SETBLOCK(STARTSCREEN+STARTROW+$120,$30,GLADE)
  MAINLOOP()
RETURN
